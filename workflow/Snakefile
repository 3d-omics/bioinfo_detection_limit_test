# Main entrypoint of the workflow.
# Please follow the best practices:
# https://snakemake.readthedocs.io/en/stable/snakefiles/best_practices.html,
# in particular regarding the standardized folder structure mentioned there.

from snakemake.utils import min_version

min_version("8.0")

import pandas as pd
import yaml

# Import configuration files
params = yaml.load(open("config/params.yml", "r"), Loader=yaml.SafeLoader)
features = yaml.load(open("config/features.yml", "r"), Loader=yaml.SafeLoader)
samples = pd.read_table("config/samples.tsv", comment="#", dtype="str")

# Generate useful variables
SAMPLES = samples["sample_id"].drop_duplicates().values.tolist()

SAMPLE_LIBRARY = samples[["sample_id", "library_id"]].values.tolist()

HOST_NAMES = [] if features["hosts"] is None else list(features["hosts"].keys())

MAG_CATALOGUES = (
    []
    if features["mag_catalogues"] is None
    else list(features["mag_catalogues"].keys())
)

KRAKEN2_DBS = list(features["databases"]["kraken2"].keys())

BAM_REPORTS = ["stats.tsv", "flagstats.txt", "idxstats.tsv"]

LAST_HOST = HOST_NAMES[-1] if len(HOST_NAMES) > 0 else None


# Import modules
module preprocess:
    snakefile:
        github("jlanga/mg_preprocess", path="workflow/Snakefile", branch="main")
    config:
        params


use rule * from preprocess as preprocess__*


# Import subworkflows
include: "rules/folders.smk"
include: "rules/mags.smk"
include: "rules/bowtie2.smk"
include: "rules/coverm.smk"
include: "rules/multiqc.smk"


rule all:
    input:
        rules.preprocess__all.input,
        rules.quantify__coverm__all.input,
        rules.quantify__multiqc__all.input,
