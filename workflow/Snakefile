# Main entrypoint of the workflow.
# Please follow the best practices:
# https://snakemake.readthedocs.io/en/stable/snakefiles/best_practices.html,
# in particular regarding the standardized folder structure mentioned there.

from snakemake.utils import min_version

min_version("8.0")

import pandas as pd
import yaml


# Import configuration files
configfile: "config/config.yml"


samples = pd.read_table(config["samples"], comment="#", dtype="str")
features = yaml.load(open(config["features"], "r"), Loader=yaml.SafeLoader)
params = yaml.load(open(config["params"], "r"), Loader=yaml.SafeLoader)

# Add missing columns
samples["assembly_ids"] = ""

# Generate useful variables

SAMPLE_LIBRARY = samples[["sample_id", "library_id"]].values.tolist()

HOST_NAMES = [] if features["hosts"] is None else list(features["hosts"].keys())

MAG_CATALOGUES = (
    []
    if features["mag_catalogues"] is None
    else list(features["mag_catalogues"].keys())
)

KRAKEN2_DBS = list(features["databases"]["kraken2"].keys())

BAM_REPORTS = ["stats.tsv", "flagstats.txt", "idxstats.tsv"]

LAST_HOST = HOST_NAMES[-1] if len(HOST_NAMES) > 0 else None


# Import modules
module mg_assembly:
    snakefile:
        github("3d-omics/mg_assembly", path="workflow/Snakefile", branch="devel")
    config:
        params


use rule * from mg_assembly exclude all, test


# Import subworkflows
include: "rules/folders.smk"
include: "rules/functions.smk"
include: "rules/quantify.smk"


rule all:
    input:
        rules.preprocess__all.input,
        rules.quantify__all.input,


rule test:
    input:
        rules.preprocess__clean__all.input,
        rules.preprocess__multiqc__all.input,
        rules.quantify__all.input,
